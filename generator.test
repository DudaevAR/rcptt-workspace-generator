--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: generator
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _S9PiAMlOEemUxIJU20rqhg
Property-repo_path: /Users/git/rcptt
Runtime-Version: 2.5.0.201908051646
Save-Time: 8/28/19 3:10 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-button "View Menu" | click
get-view "Package Explorer" | get-menu -path "Top Level Elements/Working Sets" | click
get-menu -path "Project/Build Automatically" | uncheck

map [entry "ecl" [list "ecl"]]
    [entry "core" [list "core"]]
    [entry "launching" [list "launching"]]
    [entry "runtime" [list "runtime" "testing/org.eclipse.rcptt.testing.ecl"]]
    [entry "ide" [list "rcp"]]
    [entry "watson" [list "watson"]]
    [entry "runner" [list "runner"]]
    [entry "build" [list "repository/full" "releng/target-platforms"]]| each [val projects] [val name] {

	log -message $projects
	get-button "View Menu" | click
	get-view "Package Explorer" | get-menu -path "Top Level Elements/Working Sets" | click
	get-button "View Menu" | click
	get-view "Package Explorer" | get-menu -path "Configure Working Sets..." | click
	with [get-window "Configure Working Sets"] {
	    get-button "New..." | click
	    with [get-window "New Working Set"] {
	        get-editbox -after [get-label "Working set name:"] | set-text $name
	        get-button Finish | click
	    }
	    get-button OK | click
	}

	$projects | each [val project_name] {
		set-dialog-result Folder [concat $repo_path "/" $project_name]
	
		get-menu -path "File/Import..." | click
		with [get-window Import] {
		    get-tree | select "General/Existing Projects into Workspace"
		    get-button "Next >" | click
		
		    get-button "Browse..." | click
		    with [get-group Options | get-button "Search for nested projects"] {
		        check
		    }
		    with [get-group "Working sets"] {
		        get-button "Add project to working sets" | check
		        get-button "Select..." | click
		    }
		    get-window "Select Working Sets" | get-button "Deselect All" | click
		    with [get-window "Select Working Sets"] {
		        with [get-table] {
		            get-item -path $name | check
		        }
		        get-button OK | click
		    }
		
		    get-button Finish | click
		
		    try {
		    	get-window "Creation Problems" | get-button OK | click
		    	get-button Cancel | click
		    } -catch {}
		}
	}
}


------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
